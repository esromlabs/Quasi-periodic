;http://www.calormen.com/jslogo/
TO t :s
  pendown
  ; decorate with an arc that indicates the orientation of the rhombus
  lt 2 * 36
  arc 4 * 36 :s * 0.618034 / 2
  rt 2 * 36
  
  rt 2 * 36
  forward :s
  left 4 * 36
  forward :s
  left 1 * 36
  forward :s
  left 4 * 36
  forward :s
  left 1 * 36
  lt 2 * 36
  penup
END

TO w :s
  pendown
  ; decorate with an arc that indicates the orientation of the rhombus
  lt 1 * 36
  arc 2 * 36 :s * 0.618034
  rt 1 * 36

  rt 1 * 36
  forward :s
  left 2 * 36
  forward :s
  left 3 * 36
  forward :s
  left 2 * 36
  forward :s
  left 3 * 36
  lt 1 * 36
  penup
END 

; generate rotation scale kind recursion_depth paint_all_levels
TO gen :s :k :depth :paint_all
  ifelse :depth < 1 [
    setcolor :depth
    if :k = 't' [
      t :s
    ]
    if :k = 'w' [
      w :s
    ]
  ] [
    localmake "dep :depth - 1
    localmake "scale :s * 0.618034
    
    if :k = 't' [
      ; generate a t and w
      ; first the t
      fd :scale
      rt 3 * 36
      setcolor :depth
      if :paint_all [
        t :scale
      ]
      gen :scale 't' :dep :paint_all
      
      lt 2 * 36
      fd :scale
      rt 3 * 36
      setcolor :depth
      if :paint_all [
        w :scale
      ]
      gen :scale 'w' :dep :paint_all

      rt 1 * 36
      fd :scale
      rt 1 * 36
      fd :scale

      rt 4 * 36    
    ]
    if :k = 'w' [
      ; w -> w t w
      ; first generate w
      rt 2 * 36
      fd :s
      rt 5 * 36
      setcolor :depth
      if :paint_all [
        w :scale
      ]
      gen :scale 'w' :dep :paint_all
      
      
      rt 2 * 36
      fd :scale
      lt 3 * 36
      setcolor :depth
      if :paint_all [
        t :scale
      ]
      gen :scale 't' :dep :paint_all
      
      ; last another w
      
      rt 2 * 36
      fd :scale
      rt 2 * 36
      fd :scale
      rt 4 * 36
      setcolor :depth
      if :paint_all [
        w :scale
      ]
      gen :scale 'w' :dep :paint_all
      rt 1 * 36
      fd :s * 1.618034
      lt 5 * 36
    ]
  ]
  
END
  
clearscreen
penup
setcolor "brown
fd -140
lt 90
fd 250
rt 90

rt 1 * 36
; level one test of 't'
t 200
gen 200 't' 8 0
lt 1 * 36


setcolor "brown
rt 90
fd 420
lt 90

; level one test of 'w'
w 200
gen 200 'w' 8 0
